import com.modrinth.minotaur.dependencies.ModDependency

plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
    id 'io.github.juuxel.loom-quiltflower' version '1.8.0'
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = project.archives_base_name + "-fabric"
version = project.mod_version + '-mc' + project.minecraft_version
group = project.maven_group

allprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'
    apply plugin: 'io.github.juuxel.loom-quiltflower'
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        // Mod Menu 模组
        maven { url 'https://maven.terraformersmc.com/releases/' }
        // 合理排序模组依赖的 Cloth Config
        maven { url "https://maven.shedaniel.me/" }
        /*
        这个是存储在 GitHub 上的 BRRP 模组
        maven {
            url 'https://raw.nuaa.cf/SolidBlock-cn/mvn-repo/main'
        }*/
        // 合理排序和 BRRP 模组。这两个模组均存储于本地的 maven 仓库。
        mavenLocal()
    }

    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // Fabric API
        modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}")
        // ModMenu 模组
        modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

        // 下面两个模组均存储于本地的 maven 仓库。

        // BRRP 模组
        def brrp_mc_version = project.properties.getOrDefault "brrp_mc_version", project.minecraft_version
        modImplementation "pers.solid:brrp-fabric:${project.brrp_version}-${brrp_mc_version}"
        // 合理排序模组
        def reasonable_sorting_mc_version = project.properties.getOrDefault "reasonable_sorting_mc_version", project.minecraft_version
        modImplementation "pers.solid.mod:reasonable-sorting-fabric:${project.reasonable_sorting_version}-mc${reasonable_sorting_mc_version}"

        // 由于新版的 Loom 没有 @CanIgnoreReturnValue 等注解，故在这里手动添加。
        modImplementation "com.google.errorprone:error_prone_annotations:+"
    }

    processResources {
        inputs.property "version", project.mod_version

        filesMatching("fabric.mod.json") {
            expand "version": project.mod_version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        // If Javadoc is generated, this must be specified in that task too.
        it.options.encoding = "UTF-8"

        // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 16
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}" }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
            maven {
                // 将模块导出至本地另一个项目的位置中，该项目与 GitHub 项目关联。
                // 随后，推送那个项目至 GitHub 上。
                url = 'file:../mvn-repo'
            }
        }
    }

    loom {
        runs {
            client {
                programArg "-username=Solid"
            }
        }

        runConfigs.configureEach {
            it.configName = "Minecraft Fabric ${it.name.capitalize()}"
            ideConfigGenerated = project == project(':runtest')

            runDir = project.relativePath(project(':').file('run')).toString()
        }
    }
}

subprojects {
    dependencies {
    }
}

project(':runtest') {
    dependencies {
        implementation project(path: ':', configuration: "namedElements")
    }

    remapJar {
        enabled = false
    }
}


modrinth {
    projectId = 'tb7kCxSW'
    String platform = project.properties.getOrDefault 'loom.platform', 'fabric'
    versionNumber = "${rootProject.mod_version}-${platform}-${rootProject.minecraft_version}"
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = (project.supported_minecraft_versions as String).split(',') as List<String>
    loaders = [platform]
    var list = [new ModDependency('JnrDtPAE', 'required')]
    if (platform == 'fabric') list.add new ModDependency('P7dR8mSH', 'required')
    dependencies = list
}